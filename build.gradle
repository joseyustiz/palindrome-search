plugins {
	id 'org.springframework.boot' version '2.3.1.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
	id 'groovy'

}

group = 'com.joseyustiz'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	intTestImplementation.extendsFrom testImplementation
	intTestRuntime.extendsFrom testRuntime

}

repositories {
	mavenCentral()
}

dependencies {
	implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.10'

	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
	implementation group: 'org.hibernate', name: 'hibernate-validator', version: '6.1.1.Final'
	implementation group: 'org.hibernate', name: 'hibernate-validator-annotation-processor', version: '6.1.1.Final'


	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}

	testImplementation "org.codehaus.groovy:groovy-all:2.5.8"
	testImplementation platform("org.spockframework:spock-bom:2.0-M1-groovy-2.5")
	testImplementation "org.spockframework:spock-core"
}

test {
	useJUnitPlatform()
}

//[BEGIN] Integration test configuration
sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    intTestImplementation.extendsFrom implementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
	intTestImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	intTestImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'
	intTestImplementation "org.codehaus.groovy:groovy-all:2.5.8"
	intTestImplementation platform("org.spockframework:spock-bom:2.0-M1-groovy-2.5")
	intTestImplementation "org.spockframework:spock-core"
	intTestImplementation group: 'org.spockframework', name: 'spock-spring', version: '1.3-groovy-2.5'

}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    shouldRunAfter test
    useJUnitPlatform()
}

check.dependsOn integrationTest
//[END] Integration test configuration
